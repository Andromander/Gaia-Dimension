buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.

version = project.mod_version
group = project.mod_group
archivesBaseName = project.archives_base_name

String fullVersion = project.mc_version + "-" + version
if (System.getenv("TRAVIS_BUILD_NUMBER") != null) {
    fullVersion += "+${System.getenv("TRAVIS_BUILD_NUMBER")}"
}
String jarVersion = fullVersion.replace("+", "-")

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "${project.mc_version}-${project.forge_version}"
    runDir = "run"
    replace '${version}', fullVersion
    mappings = project.forge_mappings
}

jar {
    archiveName = archivesBaseName + "-" + jarVersion + ".jar"

    classifier = 'universal'
    manifest {
        attributes 'FMLAT': 'gd_at.cfg'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveName = archivesBaseName + "-" + jarVersion + "-sources.jar"
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

repositories {
    maven {
        name = "tterrag maven"
        url = 'http://maven.tterrag.com/'
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "modmaven.k-4u.nl"
    }
}

dependencies {
    compile "team.chisel.ctm:CTM:${project.ctm_version}"
    deobfProvided "mezz.jei:jei_${mc_version}:${jei_version}:api"
    runtime "mezz.jei:jei_${mc_version}:${jei_version}"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
